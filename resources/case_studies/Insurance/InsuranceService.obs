import "resources/case_studies/Insurance/Insurer.obs"
import "resources/case_studies/Insurance/PolicyRecord.obs"

main resource contract InsuranceService {
    // TODO: Collection of Insurer, Token
    // TODO: Keys of trusted banks
    Insurer@Owned insurers;
    TimeService@Shared timeService;

    // pending and active policies
    PolicyRecord@Pending pendingPolicies;
    PolicyRecord@Active activePolicies;

    // for now have to have a reference to bank
    Bank@Shared bank;

    InsuranceService@Owned() {

    }

    transaction addInsurer(Insurer@Owned >> Unowned insurer) {
        insurers = insurer;
    }

    // TODO: information to calculate bids, Collection of Bid and Token
    transaction requestBids(int i) returns Policy@Offered {
        InsuranceBid@Owned insBid = insurers.requestBid(i);

        int cost = insBid.getCost();
        int expiration = insBid.getExpirationTime();
        Money@Owned payout = insBid.getPayout();

        Policy@Offered policy = new Policy(cost, expiration);
        Policy@Offered policyCopy = policy;

        pendingPolicies = new PolicyRecord(policy, payout);

        return policyCopy;
    }

    transaction buyPolicy(Policy@Offered >> (Active | Expired) policy, Money@Owned >> Unowned money) returns Money@Owned {

        if (timeService.getTime() > policy.getExpirationTime()) {
            // refund insurer's payout
            Money@Owned insurerRefund = pendingPolicies.refund();
            insurers.receiveRefund(insurerRefund);

            // expire policy and return money
            policy.expire();
            return money;
        } else {
            // get payment
            int cost = policy.getCost();
            Money@Owned payment = money.getAmountOfMoney(cost);

            // activate policy record with payment
            pendingPolicies.activate(payment);
            activePolicies = pendingPolicies;

            // activate policy and return any change
            policy.activate();
            return money;
        }
    }

}


