import "resources/case_studies/Insurance/InsuranceBid.obs"
import "resources/case_studies/Insurance/Bank.obs"
import "resources/case_studies/Insurance/TimeService.obs"

// TODO : Collections

main asset contract Insurer {
    Money@Owned money;
    Bank@Shared bank;
    TimeService@Shared timeService;

    Insurer@Owned(TimeService@Shared ts, Bank@Shared b, Money@Owned >> Unowned m) {
        timeService = ts;
        bank = b;
        money = m;
    }

    // TODO : Calculate Bid using some needed information, for now just int
    transaction requestBid(int longitude, int latitude, int radius, int moistureContent, int payout) returns InsuranceBid@Owned {
        // For now, assume 10% of policies will have to pay out, and we're keeping a $10 profit.
        // Obviously this is oversimplified.
        int costOfBid = payout / 10 + 10;


        // available for 24 hours
        int twentyFourHours = timeService.hoursToMillis(24);
        int expirationTime = timeService.getTime() + twentyFourHours;

        if (money.getAmount() < payout) {
            revert "Insurer does not have sufficient funds to offer another bid.";
        }

        // get correct amount of money from owned money
        Money m = money.getAmountOfMoney(payout);

        InsuranceBid b = new InsuranceBid(costOfBid, expirationTime, m);

        return b;
    }

    transaction receiveRefund(Money@Owned >> Unowned m) {
        money.addMoney(m);
    }

    transaction getRemainingBalance() returns int {
        return money.getAmount();
    }

}