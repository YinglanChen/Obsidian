//English Auction
//Money, balance, record of unowned money references, remove fee


contract Bidder {
    string name;
    int bidAmount;

    state WonItem {
        Item@Owned item;
    }
    state Bidding;

    Bidder@Owned(string n, int b) {
        bidAmount = b;
        name = n;
        -> Bidding;
    }

    transaction createBid(Bidder@Owned this) returns Bid@Owned {
        Bid bid = new Bid(bidAmount);
        return bid;
    }

    transaction getBidAmount(Bidder@Owned this) returns int {
        return bidAmount;
    }

    transaction getName(Bidder@Owned this) returns string {
        return name;
    }

    transaction won(Bidder@Owned >> WonItem this, Item@Owned >> Unowned i) {
        -> WonItem(item = i);
    }
}

contract Seller {
    Item@Owned item;
    Fee@Owned fee;
    state SoldItem {
        Bid@Owned bid;
    }
    state Unsold;

    Seller@Owned() {
        item = new Item();
        fee = new Fee();
        -> Unsold;
    }

    transaction receiveBid(Seller@Unsold >> SoldItem this, Bid@Owned >> Unowned b) {
        -> SoldItem(bid = b);
    }

    transaction getBackFee(Seller@Unsold this, Fee@Owned >> Unowned f) {
        fee = f;
    }
}

contract Item {

}

contract Fee {

}

contract Bid {
    int amount;
    Bid@Owned(int num) {
        amount = num;
    }

    transaction getAmount(Bid@Owned this) returns int {
        return amount;
    }
}


main contract Auction {

    Bidder@Owned maxBidder;
    Seller@Owned seller;

    state Open {
        Item@Owned item;
        Bid@Owned bid;
        Fee@Owned fee;
    }
    state BiddingDone {
        Item@Owned it;
        Bid@Owned finalBid;
    }
    state Closed;


    Auction@Owned() {
        maxBidder = new Bidder("none", 0);
        seller = new Seller();
        -> Closed;
    }

    transaction receiveItemFromSeller(Auction@Closed >> Open this, Item@Owned >> Unowned i, Fee@Owned >> Unowned f) {
        Bid b = new Bid(0);
        -> Open(item = i, bid = b, fee = f);
    }

    transaction makeBid(Auction@Open this, Bidder@Owned >> Unowned bidder) {
        if (bidder.getBidAmount() > bid.getAmount()) {
            bid = bidder.createBid();
            maxBidder = bidder;
        }
    }

    transaction closeBidding(Auction@Open >> BiddingDone this) {
        if (bid.getAmount() <= 0) {
            if (seller in Unsold) {
                seller.getBackFee(fee);
            }
        }
        -> BiddingDone(it = item, finalBid = bid);
    }

    transaction giveItem(Auction@BiddingDone >> Closed this) {
        maxBidder.won(it);
        if (seller in Unsold) {
            seller.receiveBid(finalBid);
        }
        -> Closed;
    }

}