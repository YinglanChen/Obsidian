contract Player {
    state ToChoose;
    state Rock;
    state Paper;
    state Scissors;

    Player() {
        ->ToChoose;
    }

    transaction choose(Player@Owned >> (Rock | Paper | Scissors) this, string choice) {
        if (choice == "rock") {
            ->Rock;
        } else {
            if (choice == "paper") {
                ->Paper;
            } else {
                if (choice == "scissors") {
                    ->Scissors;
                } else {
                    revert "choice must be one of: rock, paper, scissors";
                }
            }
        }
    }

    transaction beats(Player@(Rock | Paper | Scissors) this, Player@(Rock | Paper | Scissors) opponent) returns int {
        if (this in Rock) {
            if (opponent in Rock) {
                return 0;
            } else {
                if (opponent in Scissors) {
                    return 1;
                } else {
                    return -1;
                }
            }
        } else {
            if (this in Scissors) {
                if (opponent in Scissors) {
                    return 0;
                } else {
                    if (opponent in Paper) {
                        return 1;
                    } else {
                        return -1;
                    }
                }
            } else {
                if (opponent in Paper) {
                    return 0;
                } else {
                    if (opponent in Rock) {
                        return 1;
                    } else {
                        return -1;
                    }
                }
            }
        }
    }

    transaction rock(Player@Owned >> Unowned this) returns Player@Rock {
        ->Rock;
        return this;
    }
}

main contract RockPaperScissors {
    state GameStart;
    state PlayerOneChose;
    state PlayerTwoChose;
    state PlayerOneWin;
    state PlayerTwoWin;
    state Tie;

    Player@(Rock | Paper | Scissors) playerOne available in PlayerOneChose, PlayerTwoChose;
    Player@(Rock | Paper | Scissors) playerTwo available in PlayerTwoChose;

    RockPaperScissors() {
        PlayerOneChose::playerOne = new Player().rock();
        ->PlayerOneChose;
        PlayerTwoChose::playerTwo = new Player().rock();
        ->PlayerTwoChose;
        ->GameStart;
    }

    transaction playerOneChoose(RockPaperScissors@GameStart >> PlayerOneChose this, string choice) {
        Player p = new Player();
        p.choose(choice);
        ->PlayerOneChose(playerOne = p);
    }

    transaction playerTwoChoose(RockPaperScissors@PlayerOneChose >> PlayerTwoChose this, string choice) {
        Player p = new Player();
        p.choose(choice);
        ->PlayerTwoChose(playerTwo = p);
    }

    transaction result(RockPaperScissors@PlayerTwoChose >> (PlayerOneWin | PlayerTwoWin | Tie) this) {
        int res = playerOne.beats(playerTwo);

        if (res == 1) {
            ->PlayerOneWin;
        } else {
            if (res == -1) {
                ->PlayerTwoWin;
            } else {
                ->Tie;
            }
        }
    }

    transaction reset(RockPaperScissors@Owned >> GameStart this) {
        ->GameStart;
    }
}

