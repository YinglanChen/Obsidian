<!DOCTYPE HTML>
<html lang="en">
<head>
    <title>Part 1: Ownership and Assets</title>
    <meta charset="utf-8"  />
    <link rel="stylesheet" href="../stylesheet.css"/>
    <link href="../prism.css" rel="stylesheet" />
</head>
<body class="language-obsidian">
<script src="../prism.js"></script>
<h2><strong>Part 1: Ownership and Assets</strong></h2>
 <!-- local variables never include annotatations. Params and fields always do. -->
<p>Our new programming language is object-oriented. It includes <em>contracts</em>, which are like classes, which can have <em>fields</em>. In addition, of the many <strong>variables</strong> or <strong>fields</strong> that reference objects, <strong>one</strong> of them can own the object. For example:</p>

<pre>
<code>
contract Money {
}

<strong>contract</strong> Wallet {
    Money@Owned m; // @Owned indicates that m owns the object it refers to
}
</code>
</pre>

<p>The compiler tracks ownership of each variable every time the variable is used. This information is part of the <em>type</em> of the variable. For example, the type of <code>m</code> is <code>Money@Owned</code>. Information about ownership is NOT available at runtime; it is only available during compilation.</p>

<ul>
    <li>If a reference is the only one that holds ownership, then it is <code>Owned</code>.</li>
    <li>If all references to the object are the same (there is no owner), then each reference is <code>Shared</code>.</li>
    <li>If a reference is NOT the owning one, but there might be another owning reference, then the reference is <code>Unowned</code>.</li>
</ul>



<h2><strong>Assets</strong></h2>

<p>Some owning references are to things that should not be accidentally lost. To prevent accidental loss, we can declare contracts with the keyword <code>asset</code>. Let's do this for <code>Money</code>:</p>

<pre>
<code>
asset contract Money {
}
</code>
</pre>

<p>Now, if we accidentally lose track of an owning reference to a <code>Money</code> object (by letting it go out of scope without transferring ownership to somewhere else, such as a field), the compiler will give an error:

<pre>
<code>
transaction test() {
    Money m = ...; [m@Owned]; // OK, m is Owned here
    // ERROR: cannot drop reference to owned asset m
}
</code>
</pre>

We can fix this by (for example) returning m, assigning it to an owning field, or passing it as an argument to an appropriate transaction. For example:

<pre>
<code>
transaction test() returns Money@Owned {
    Money m = ...; [m@Owned]; // OK, m is Owned here
    return m; // gives ownership of m to the caller of test()
}
</code>
</pre>

<p>Note that non-owning references to <code>Money</code> are not restricted; the compiler gives no errors when they go out of scope.</p>

<h3>Method return types</h3>
<p>
When transactions return objects, the types must be annotated. For example:
<pre>
<code>
transaction withdraw() returns Money@Owned {
    // body not shown
}
</code>
</pre>
</p>

<h3>Method parameters</h3>
<p>When a reference is passed to a transaction as an argument, the transaction's declaration specifies initial and final ownership with <code>>></code>. For example:</p>

<pre>
<code>
transaction spend(Money@Owned >> Unowned m) { // m is Owned initially but must be Unowned at the end.
	// implementation not shown
};

transaction testSpend(Money@Owned >> Unowned m) {
	spend(m);
	// m is now of type Money@Unowned due to specification on spend() declaration.
}
</code>
</pre>

<p>If a transaction expects an argument that is <code>Unowned</code>, this means that the transaction cannot take ownership. As a result, it is safe to pass an <code>Owned</code> reference as an argument to a transaction that expects an <code>Unowned</code> argument. After the transaction returns, the caller still holds ownership.</p>

<p>If a transaction parameter lacks >>, then ownership will not change. For example, <code>transaction test(Money@Owned m)</code> is equivalent to <code>transaction test(Money@Owned >> Owned m)</code>. <code>transaction foo(Money@Unowned m)</code> can accept a <code>Money</code> reference with any ownership and the caller maintains whatever ownership it had initially when it called that transaction.


<h3>Ownership checks</h3>
<p>Ownership can be documented and checked by writing the ownership in brackets. For example, <code>[m@Owned]</code> indicates that <code>m</code> is an owning reference at that particular point in the code. The compiler will generate an error if this might not be the case. For example, with <code>spend</code> as above:</p>

<pre>
<code>
transaction testSpend(Money@Owned >> Unowned  m) {
	spend(m); [m@Unowned]; // OK
	[m@Owned]; // COMPILE ERROR: m is Unowned here
}
</code>
</pre>

<p>Ownership checks in <code>[]</code> <strong>never</strong> change ownership; they only document and check what is known.</p>

<h3>Assignment</h3>
<p>Assignment (<code>=</code>) transfers ownership. That is, the variable on the left becomes the <strong>new</strong> owner, and the variable on the right loses ownership. For example:</p>

<pre>
<code>
Money m = ...; [m@Owned];
Money newMoney = m; [m@Unowned];
// Now m is of type Money@Unowned, not Money@Owned.
</code>
</pre>

<h3>Invoking transactions</h3>
<p> The compiler checks each invocation to make sure it is permitted according to the ownership of the transaction arguments. For example:</p>

<pre>
<code>
transaction spend(Money@Owned >> Unowned m) {
	// implementation not shown
};
transaction print(Money@Unowned m) {
    // implementation not shown
}

transaction test() {
	Money m = ... // assume m is now owned.
	print(m); [m@Owned];
	spend(m); [m@Unowned];
	spend(m); // COMPILE ERROR: spend() requires Owned input, but m is Unowned here
}
</code>
</pre>

<h3>Local variables</h3>
Local variables are declared <strong>without</strong> any ownership specified; instead, you can optionally write compiler ownership checks <code>[]</code> to specify their ownership. The compiler will output an error if the ownership does not match the check. For example:
<pre>
<code>
transaction withdraw() returns Money@Owned {
    // body not shown
}

transaction test() {
    Money m = withdraw(); [m@Owned];
    spend(m); [m@Unowned];
    [m@Owned]; // COMPILE ERROR: m is Unowned here
</code>
</pre>

<h3>Getting rid of ownership</h3>
<p>If ownership is no longer desired, <code>disown</code> can be used to relinquish ownership. For example:</p>
<pre>
<code>
contract Money {
    int amount;

    transaction merge(Money@Owned >> Unowned mergeFrom) {
        amount += mergeFrom.amount;
        disown mergeFrom; // We absorbed the value of mergeFrom, so the owner doesn't own it anymore.
    }
}
</code>
</pre>

<h3>Method receivers (<code>this</code>)</h3>
<p>Sometimes the ownership of <code>this</code> needs to change in a transaction. That fact can be specified by adding <code>this</code> as the first argument in the transaction declaration. For example:</p>

<pre>
<code>
contract Money {
    transaction discard(Money@Owned >> Unowned this) {
        disown this;
    }
}

contract Wallet {
    transaction throwAwayMoney(Money @ Owned >> Unowned money) {
        money.discard(); // 'this' argument is implicit; do not include it in transaction call.
    }
}
</code>
</pre>

<h3>Fields</h3>
<p><strong>Field declarations</strong>&nbsp;MUST include ownership specifications. These mean that&nbsp;at the END of each transaction, the state of the object the field references MUST match the annotation on the field. For example:</p>
<br>
<pre>
<code>
contract Wallet {
	Money@Owned money; // Note that this annotation is on a field declaration!

	// exchange removes (and returns) the current contents of the Wallet, replacing it with the input Money.
	transaction exchange(Money@Owned >> Unowned otherMoney) returns Money@Owned {
        Money temporaryMoney = money; [money@Unowned]; // OK temporarily...
        money = otherMoney; [money@Owned]; // as long as money@Owned is restored by the end of the transaction.
        return temporaryMoney;
	}
}
</code>
</pre>




</body></html>
