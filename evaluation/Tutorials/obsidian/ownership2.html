<!DOCTYPE HTML>
<html lang="en">
<head>
    <title>Ownership: Part 2</title>
    <meta charset="utf-8"  />
    <link rel="stylesheet" href="../stylesheet.css"/>
    <link href="../prism.css" rel="stylesheet" />
</head>
<body class="language-obsidian">
<script src="../prism.js"></script>
<h2><strong>Part 2: Ownership Continued</strong></h2>


<h3>Ownership checks</h3>
<p>Ownership can be documented and checked by writing the ownership in brackets. For example, <code>[m@Owned]</code> indicates 
    that <code>m</code> is an owning reference at that particular point in the code. The compiler will generate an error if this 
    might not be the case. For example:
</p>

<pre>
<code>

transaction spend(Money@Owned >> Unowned m) { // m is Owned initially but must be Unowned at the end.
    // implementation not shown
}

transaction testSpend(Money@Owned >> Unowned  m) {
	spend(m); [m@Unowned]; // OK
	[m@Owned]; // COMPILE ERROR: m is Unowned here
}
</code>
</pre>

<p>Ownership checks in <code>[]</code> <strong>never</strong> change ownership; they only document and check what is known.</p>

<h3>Assignment</h3>
<p>Assignment (<code>=</code>) transfers ownership. That is, the variable on the left becomes the <strong>new</strong> owner, and the variable on the right loses ownership. For example:</p>

<pre>
<code>
Money m = ...; [m@Owned];
Money newMoney = m; [m@Unowned];
// Now m is of type Money@Unowned, not Money@Owned.
</code>
</pre>

<h3>Fields</h3>
<p><strong>Field declarations</strong>&nbsp;MUST include ownership specifications. This means that&nbsp;at the END of each transaction, the state of the object the field references MUST match the annotation on the field. For example:</p>
<pre>
<code>
contract Wallet {
	Money@Owned money; // Note that this annotation is on a field declaration!

	// exchange removes (and returns) the current contents of the Wallet, replacing it with the input Money.
	transaction exchange(Money@Owned >> Unowned otherMoney) returns Money@Owned {
        Money temporaryMoney = money; [money@Unowned]; // OK temporarily...
        money = otherMoney; [money@Owned]; // as long as money@Owned is restored by the end of the transaction.
        return temporaryMoney;
	}
}
</code>
</pre>

<h3>Local variables</h3>
Local variables are declared <strong>without</strong> any ownership specified; instead, you can optionally write compiler ownership checks <code>[]</code> to specify their ownership. The compiler will output an error if the ownership does not match the check. For example:
<pre>
<code>
transaction withdraw() returns Money@Owned {
    // body not shown
}

transaction test() {
    Money m = withdraw(); [m@Owned];
    spend(m); [m@Unowned];
    [m@Owned]; // COMPILE ERROR: m is Unowned here
</code>
</pre>

<h3>Getting rid of ownership</h3>
<p>If ownership is no longer desired, <code>disown</code> can be used to relinquish ownership. For example:</p>
<pre>
<code>
contract Money {
    int amount;

    transaction merge(Money@Owned >> Unowned mergeFrom) {
        amount += mergeFrom.amount;
        disown mergeFrom; // We absorbed the value of mergeFrom, so the owner doesn't own it anymore.
    }
}
</code>
</pre>

<h3>Invoking transactions</h3>
<p> The compiler checks each invocation to make sure it is permitted according to the ownership of the transaction arguments. For example:</p>

<pre>
<code>
transaction spend(Money@Owned >> Unowned m) {
	// implementation not shown
};
transaction print(Money@Unowned m) {
    // implementation not shown
}

transaction test() {
	Money m = ... // assume m is now owned.
	print(m); [m@Owned];
	spend(m); [m@Unowned];
	spend(m); // COMPILE ERROR: spend() requires Owned input, but m is Unowned here
}
</code>
</pre>



</body></html>